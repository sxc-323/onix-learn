# 硬盘读写
- LBA模式 以及 CHS模式
- 扇区：512个字节，读取扇区，最小一个，最多256个扇区。

# IDE/ATA PIO Mode

- 端口是外部设备内部的寄存器。
- IDE:Integrated Drive Electronics
- ATA:Advanced Technologe Attachment
- ANSI:American National Standards Institute
- SATA

- CHS：Cylinder/Header/Sector 
- LBA：Logical Block Address 起始位置是0扇区

LBA28： 2^28*512B的位置：

硬盘控制端口

| Primary 通道            | Secondary 通道 | in 操作      | out 操作     |
| ----------------------- | -------------- | ------------ | ------------ |
| 0x1F0                   | 0x170          | Data         | Data         |
| 0x1F1                   | 0x171          | Error        | Features     |
| 0x1F2                   | 0x172          | Sector count | Sector count |
| 0x1F3                   | 0x173          | LBA low      | LBA low      |
| 0x1F4                   | 0x174          | LBA mid      | LBA mid      |
| 0x1F5                   | 0x175          | LBA high     | LBA high     |
| 0x1F6                   | 0x176          | Device       | Device       |
| 0x1F7                   | 0x177          | Status       | Command      |

- 0x1F0：16bit 端口，用于读写数据
- 0x1F1：检测前一个指令的错误
- 0x1F2：读写扇区的数量
- 0x1F3：起始扇区的 0 ~ 7 位
- 0x1F4：起始扇区的 8 ~ 15 位
- 0x1F5：起始扇区的 16 ~ 23 位
- 0x1F6:
    - 0 ~ 3：起始扇区的 24 ~ 27 位
    - 4: 0 主盘, 1 从片
    - 6: 0 CHS, 1 LBA
    - 5 ~ 7：固定为1
- 0x1F7: out
    - 0xEC: 识别硬盘
    - 0x20: 读硬盘
    - 0x30: 写硬盘
- 0x1F7: in / 8bit
    - 0 ERR
    - 3 DRQ 数据准备完毕
    - 7 BSY 硬盘繁忙


## boot.asm中 读写硬盘的写法
```s

mov edi,0x1000; 读取的目标内存
mov ecx,0; 起始扇区
mov bl,1; 扇区数量
; 上面这段既可用于读内存，也可用于写内存

read_disk:
    mov dx, 0x1f2
    mov al,bl
    out dx,al

    inc dx  ;0x1f3
    mov al,cl
    out dx,al

    inc dx  ;0x1f4
    shr ecx,8
    mov al,cl
    out dx,al

    inc dx  ;0x1f5
    shr ecx,8
    mov al,cl
    out dx,al

    inc dx  ;0x1f6
    shr ecx,8
    and cl,0b1111;将高四位设为0

    or cl,0b1110_0000
    mov al,cl
    out dx,al;主盘 LBA模式

    inc dx  
    mov al,0x20
    out dx,al

    xor ecx,ecx
    mov cl,bl; 得到读取扇区的数量

    .read:
        push cx
        call .waits
        call .reads
        pop cx
        loop .read
    ret

    .waits:
        mov dx,0x1f7
        .check:
            in al,dx
            jmp $+2
            jmp $+2
            jmp $+2
            and al,0b1000_1000
            cmp al,0b0000_1000
            jnz .check
        ret
    
    .reads
        mov dx,0x1f0
        mov cx,256
        .readw:
            in ax,dx
            jmp $+2 ;增加延时
            jmp $+2
            jmp $+2
            mov [edi],ax
            add edi,2
            loop .readw
        ret

write_disk:
    mov dx, 0x1f2
    mov al,bl
    out dx,al

    inc dx  ;0x1f3
    mov al,cl
    out dx,al

    inc dx  ;0x1f4
    shr ecx,8
    mov al,cl
    out dx,al

    inc dx  ;0x1f5
    shr ecx,8
    mov al,cl
    out dx,al

    inc dx  ;0x1f6
    shr ecx,8
    and cl,0b1111;将高四位设为0

    or cl,0b1110_0000
    mov al,cl
    out dx,al;主盘 LBA模式

    inc dx  
    mov al,0x30
    out dx,al

    xor ecx,ecx
    mov cl,bl; 得到读取扇区的数量

    .write:
        push cx
        call .writes
        call .waits
        pop cx
        loop .write
    ret

    .waits:
        mov dx,0x1f7
        .check:
            in al,dx
            jmp $+2
            jmp $+2
            jmp $+2
            and al,0b1000_0000
            cmp al,0b0000_0000
            jnz .check
        ret
    
    .writes
        mov dx,0x1f0
        mov cx,256
        .writew:
            mov ax,[edi]
            out dx,ax
            jmp $+2
            jmp $+2
            jmp $+2
            add edi,2
            loop .writew
        ret
```